### Plot spectrogram and formants in Praat
### by Jordan L. Manes
### Boston University
### Feb 1, 2021


#############################################
## FORM PROMPT

form Fill 
	comment Subject ID:
		text Subject test22
	comment Session:
		text Session 1
	comment Run:
		text Run 4
	comment Trial Number:
		text Trial 1
	choice Condition: 1
		button U1 (F1 Shift Up)
		button D1 (F1 Shift Down)
		button N1 (F1 No Shift)
		button U0 (F0 Shift Up)
		button D0 (F0 Shift Down)
		button N0 (F0 No Shift)
		button Base (Baseline)
endform

#############################################
## VARIABLE DEFINITIONS
	
## Define variables from form
	sub$=subject$
	ses$=session$
	run$=run$
	trial$=trial$
	cond$=condition$

## Define mic and headphone object names
	head$ = "sub-"+sub$+"_ses-"+ses$+"_run-"+run$+"_task-auditory_trial-"+trial$+"_headphones"
	mic$ = "sub-"+sub$+"_ses-"+ses$+"_run-"+run$+"_task-auditory_trial-"+trial$+"_mic"

#############################################
## EXTRACT ACOUSTICS

	## Get spectrograms
		selectObject: "Sound " + head$
			To Spectrogram: 0.005, 5000, 0.002, 20, "Gaussian"
		selectObject: "Sound " + mic$
			To Spectrogram: 0.005, 5000, 0.002, 20, "Gaussian"

	## Get formants traces
		selectObject: "Sound " + head$
			To Formant (burg): 0, 5, 5500, 0.025, 50
		selectObject: "Sound " + mic$
			To Formant (burg): 0, 5, 5500, 0.025, 50

	## Get intensity traces
		selectObject: "Sound " + mic$
			To Intensity: 100, 0, "no"
		selectObject: "Sound " + head$
			To Intensity: 100, 0, "no"

	## Get pitch traces
		selectObject: "Sound " + mic$
			To Pitch: 0, 75, 600
		selectObject: "Sound " + head$
			To Pitch: 0, 75, 600
	
#############################################
## MAIN FIGURE

## FIGURE TEXT

		## Label trial info
			Select outer viewport: 0, 2, 0.5, 0.75
				Black
				Text: 0, "Left", 0, "Half", "Subject: " + sub$
			Select outer viewport: 0, 2, 0.75, 1
				Text: 0, "Left", 0, "Half", "Session: " + ses$ + "   Run: " + run$ + "   Trial: " + trial$
			Select outer viewport: 0, 2, 1, 1.25
				Text: 0, "Left", 0, "Half", "Condition: " + cond$

		## Color legend
			Select outer viewport: 3, 5, 0.5, 0.75
				Black
				Text: 0, "Left", 0, "Half", "Color legend:"
			Select outer viewport: 3, 5, 0.75, 1	
				Blue
				Text: 0, "Left", 0, "Half", "Microphone traces"
			Select outer viewport: 3, 5, 1, 1.25
				Red
				Text: 0, "Left", 0, "Half", "Heaphone traces"

## FIGURE PLOTS - FORMANTS

		## Plot formant traces over mic spectrogram
			Select outer viewport: 3, 3.5, 1.5, 2
				Black
				Text: 0, "Centre", 0, "Half", "Microphone Spectrogram"
			Select outer viewport: 0, 6, 1.5, 6
				selectObject: "Spectrogram " + mic$
					Paint: 0, 2.5, 0, 5000, 100, "yes", 50, 6, 0, "yes"
				selectObject: "Formant " + mic$
					Blue
					Draw tracks: 0, 2.5, 5000, "no"
				selectObject: "Formant " + head$
					Red
					Draw tracks: 0, 2.5, 5000, "no"
				Marks left: 6, "yes", "yes", "no"
				Marks bottom every: 1, 0.5, "yes", "yes", "no"

		## Plot formant traces over headphone spectrogram
			Select outer viewport: 9, 9.5, 1.5, 2
				Black
				Text: 0, "Centre", 0, "Half", "Headphone Spectrogram"
			Select outer viewport: 6, 12, 1.5, 6
				selectObject: "Spectrogram " + head$
					Paint: 0, 2.5, 0, 5000, 100, "yes", 50, 6, 0, "yes"
				selectObject: "Formant " + mic$
					Blue
					Draw tracks: 0, 2.5, 5000, "no"
				selectObject: "Formant " + head$
					Red
					Draw tracks: 0, 2.5, 5000, "no"
				Marks left: 6, "yes", "yes", "no"
				Marks bottom every: 1, 0.5, "yes", "yes", "no"

## FIGURE PLOTS - INTENSITY

		## Plot intensity traces
			Select outer viewport: 3, 3.5, 6, 6.5
				Black
				Text: 0, "Centre", 0, "Half", "Intensity traces"
			Select outer viewport: 0, 6, 6, 10.5
				Blue
				selectObject: "Intensity " + mic$
				Draw: 0, 2.5, 0, 95, "yes"
			Select outer viewport: 0, 6, 6, 10.5
				Red
				selectObject: "Intensity " + head$
				Draw: 0, 2.5, 0, 95, "no"
			Marks bottom every: 1, 0.5, "yes", "yes", "no"
			Marks left every: 1, 5, "yes", "yes", "no"

## FIGURE PLOTS - PITCH

		## Plot pitch traces
			Select outer viewport: 9, 9.5, 6,6.5
				Black
				Text: 0, "Centre", 0, "Half", "Pitch traces"
			Select outer viewport: 5.5, 12, 6, 10.5
				Blue
				selectObject: "Pitch " + mic$
				Draw: 0, 2.5, 0, 500, "yes"
			Select outer viewport: 5.5, 12, 6, 10.5
				Red
				selectObject: "Pitch " + head$
				Draw: 0, 2.5, 0, 500, "no"
			Marks bottom every: 1, 0.5, "yes", "yes", "no"
			Marks left every: 1, 100, "yes", "yes", "no"

#############################################
## SAVE AND EXPORT FIGURE

## Define directories
	projectDir$ = "/Users/jordanmanes/GitHub/SAP-PILOT"
	fileDir$ = projectDir$ + "/derivatives/praat/QAplots/"

## Define file name
	fileName$ = "plot_sub-" + sub$ + "_ses-" + ses$ + "_run-" + run$ + "_trial-" + trial$

## Select whole figure
	Select outer viewport: 0, 12, 0, 10.5

##Save Praat picture file
	Save as praat picture file: fileDir$ + "/" + fileName$ + ".prapic"

## Save as .png
	Save as 300-dpi PNG file: fileDir$ + "/" + fileName$ + ".png"

Erase all
#############################################



